// ZipHexEbcdic/build.gradle
apply plugin: 'groovy'
apply plugin: 'application'

sourceCompatibility = 1.6
targetCompatibility = 1.6
defaultTasks 'clean', 'build','jar', 'test', 'groovydoc', 'javadoc', 'distTar', 'distZip', 'run', 'runSimple' 
mainClassName = 'ToHex'


// prepare for gradle 2.0 - wrap defs in project.ext closure
project.ext {
	// current EBCDIC version
	version = '1.0'
	group = 'groovyx.ebcdic'
	title = "Ebcdic Decoder"
}


repositories {
    mavenCentral()
    //add repositories for optional dependencies
    maven { url  "http://repository.jboss.org/maven2/" }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.7"
    testCompile "junit:junit:4.11"
}

jar {
    baseName 'ebcdic'
    manifest {
        attributes 'Implementation-Title': 'Ebcdic', 'Implementation-Version': '0.2'
    }
    metaInf { from 'src/main/META-INF' }
}

groovydoc {
    def title = "Ebcdic ${version}"
    windowTitle title
    docTitle title
    link 'http://java.sun.com/javaee/6/docs/api/', 'javax.servlet.'
    link 'http://java.sun.com/j2se/1.6.0/docs/api', 'java.,org.xml.,org.xml.'
    link 'http://docs.groovy-lang.org/latest/html/gapi/', 'org.codehaus.groovy.,groovy.'
}


// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
     baseName 'ebcdic'
     classifier 'sources'
     from sourceSets.main.allSource
}
 
task javadocJar(type: Jar, dependsOn:groovydoc) {
     baseName 'ebcdic'
     classifier 'javadoc'
     //from apiDir
}
 
// add javadoc/source jar tasks as artifacts
artifacts {
     archives sourcesJar
     archives javadocJar
}


task(runSimple, dependsOn: 'classes', type: JavaExec) {
	main = 'ToHex'
	classpath = sourceSets.main.runtimeClasspath
	args 'jimbo', 'hi kids'
	systemProperty 'simple.message', 'Hi kids '
}

// testing  parameters
test {
    maxParallelForks = Runtime.getRuntime().availableProcessors()
    testLogging {
        exceptionFormat = 'full'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion "2.14"
}